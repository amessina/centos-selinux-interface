
## <summary>policy for lldpad</summary>

########################################
## <summary>
##	Transition to lldpad.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`lldpad_domtrans',`
	gen_require(`
		type lldpad_t, lldpad_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, lldpad_exec_t, lldpad_t)
')


########################################
## <summary>
##	Execute lldpad server in the lldpad domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`lldpad_initrc_domtrans',`
	gen_require(`
		type lldpad_initrc_exec_t;
	')

	init_labeled_script_domtrans($1, lldpad_initrc_exec_t)
')


########################################
## <summary>
##	Search lldpad lib directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`lldpad_search_lib',`
	gen_require(`
		type lldpad_var_lib_t;
	')

	allow $1 lldpad_var_lib_t:dir search_dir_perms;
	files_search_var_lib($1)
')

########################################
## <summary>
##	Read lldpad lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`lldpad_read_lib_files',`
	gen_require(`
		type lldpad_var_lib_t;
	')

	files_search_var_lib($1)
	read_files_pattern($1, lldpad_var_lib_t, lldpad_var_lib_t)
')

########################################
## <summary>
##	Manage lldpad lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`lldpad_manage_lib_files',`
	gen_require(`
		type lldpad_var_lib_t;
	')

	files_search_var_lib($1)
	manage_files_pattern($1, lldpad_var_lib_t, lldpad_var_lib_t)
')

########################################
## <summary>
##	Manage lldpad lib directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`lldpad_manage_lib_dirs',`
	gen_require(`
		type lldpad_var_lib_t;
	')

	files_search_var_lib($1)
	manage_dirs_pattern($1, lldpad_var_lib_t, lldpad_var_lib_t)
')


########################################
## <summary>
##	Read lldpad PID files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`lldpad_read_pid_files',`
	gen_require(`
		type lldpad_var_run_t;
	')

	files_search_pids($1)
	allow $1 lldpad_var_run_t:file read_file_perms;
')

#####################################
## <summary>
##      Send to a lldpad unix dgram socket.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed access.
##      </summary>
## </param>
#
interface(`lldpad_dgram_send',`
        gen_require(`
                type lldpad_t;
        ')

        allow $1 lldpad_t:unix_dgram_socket sendto;
		allow lldpad_t $1 :unix_dgram_socket sendto;
')

########################################
## <summary>
##	All of the rules required to administrate
##	an lldpad environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`lldpad_admin',`
	gen_require(`
		type lldpad_t;
	type lldpad_initrc_exec_t;
	type lldpad_var_lib_t;
	type lldpad_var_run_t;
	')

	allow $1 lldpad_t:process { ptrace signal_perms };
	ps_process_pattern($1, lldpad_t)

	lldpad_initrc_domtrans($1)
	domain_system_change_exemption($1)
	role_transition $2 lldpad_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_var_lib($1)
	admin_pattern($1, lldpad_var_lib_t)

	files_search_pids($1)
	admin_pattern($1, lldpad_var_run_t)

')

